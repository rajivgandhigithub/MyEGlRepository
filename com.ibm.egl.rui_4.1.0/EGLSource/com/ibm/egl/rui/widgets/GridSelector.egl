/*
 * Licensed Materials - Property of IBM
 *
 * Copyright IBM Corporation 2008, 2011. All Rights Reserved.
 *
 * U.S. Government Users Restricted Rights - Use, duplication or
 * disclosure restricted by GSA DP Schedule Contract with IBM Corp.
 */
package com.ibm.egl.rui.widgets;

Delegate
	GridSelectionListener()
end

handler GridSelector type BasicHandler

	selection any[] = [];
	selectedRows Widget[] = [];
	color String = "lightgreen";
	
	selectionChangedListener GridSelectionListener = null;
	
	function enableSelection(grid Grid in, td Widget in, row any in, rowNumber int in, column GridColumn in) 
		td.onClick ::= setSelected;
		td.setAttribute("originalBG", td.backgroundColor);
		td.setAttribute("rowNumber", rowNumber);
		grid.cursor = "pointer";
	end
	
	function setSelected(e Event in)
		td Widget = e.widget;
		tr Widget = td.parent;
		grid Grid = td.logicalParent;
		dataRecord any = grid.data[td.getAttribute("rowNumber") as int];
		if (e.ctrlKey || e.metaKey)
			index int = selectionIndex(tr);
			if (index == 0)
				selection.appendElement(dataRecord);
				selectedRows.appendElement(tr);
				highlightRow(tr);
			else
				unHighlightRow(tr);
				selection.removeElement(index);
				selectedRows.removeElement(index);
			end
		else
			unHighlightRows(selectedRows);
			selection = [ dataRecord ];
			selectedRows = [ tr ];
			highlightRow(tr);
		end
		if (selectionChangedListener != null)
			selectionChangedListener();
		end
	end	
	
	function selectionIndex(tr Widget in) returns(int)
		for (n int from 1 to selectedRows.getSize() by 1)
			if (selectedRows[n] == tr)
				return(n);
			end
		end
		return (0);
	end	
	
	function unHighlightRows(trs Widget[] in)
		for (r int from 1 to trs.getSize() by 1)
			unHighlightRow(trs[r]);
		end
	end
	
	function unHighlightRow(tr Widget in)
		tds Widget[] = tr.getElementsByTagName("td");
		for (n int from 1 to tds.getSize() by 1)
			if (tds[n].class != "EglRuiGridHeader")
				tds[n].backgroundColor = tds[n].getAttribute("originalBG");
			end
		end
		tr.cursor = "default";
	end
	
	function highlightRow(tr Widget in)
		tds Widget[] = tr.getElementsByTagName("td");
		for (n int from 1 to tds.getSize() by 1)
			tds[n].backgroundColor = color;
		end
		tr.cursor = "move";
	end
	
	function clear(Grid grid)
		unHighlightRows(grid.getElementsByTagName("tr"));
		selection = [ ];
		selectedRows = [ ];
	end
end
