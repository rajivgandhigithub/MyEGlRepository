/*
 * Licensed Materials - Property of IBM
 *
 * Copyright IBM Corporation 2008, 2011. All Rights Reserved.
 *
 * U.S. Government Users Restricted Rights - Use, duplication or
 * disclosure restricted by GSA DP Schedule Contract with IBM Corp.
 */
package com.ibm.egl.rui.widgets;

handler BIDITextField type RUIWidget{tagName="input", onConstructionFunction = start, cssFile = "css/com.ibm.egl.rui_4.1.0.css",
	@VEWidget{
		category = "Display and Input",
		template = "${typeName}{}",
		smallIcon = "icons/ctool16/textfield.gif",
		largeIcon = "icons/ctool24/textfield.gif"
	}}

	text String { @EGLProperty { getMethod=getText, setMethod=setText }, @VEProperty{} };
	readOnly boolean {@EGLProperty {getMethod=getReadOnly, setMethod=setReadOnly}, @VEProperty{}};
	
	private reverseTextDirectionThis String = "No";
	private widgetOrientationThis String = "";
	private textLayoutThis String = "Logical";
	private handlers VisualHandlers = null;
				
	error boolean = false;
	
	private savedBackgroundColor string;
	private savedEnableSelection boolean;
	
	function start()
		onFocusGained ::= enableSelection;
		onFocusLost ::= disableSelection;
		borderWidth = 1;
		borderStyle = "solid";
		borderColor = "#7f9db9";
	end
	
	function enableSelection(e Event in)
		savedEnableSelection = RUILib.getTextSelectionEnabled();
		RUILib.setTextSelectionEnabled(true);
	end
	
	function disableSelection(e Event in)
		RUILib.setTextSelectionEnabled(savedEnableSelection);
	end
	
	private function getReadOnly() returns(boolean)
		return(readOnly);
	end
	
	private function setReadOnly(ro boolean in)
		readOnly = ro;
		if(readOnly)
			setAttribute("readOnly", "true");
			savedBackgroundColor = backgroundColor;
			backgroundColor = null;
			class = "EglRuiBIDITextFieldReadOnly";
		else
			setAttribute("readOnly", "");
			backgroundColor = savedBackgroundColor;
			class = "EglRuiBIDITextField";
		end
	end

	private function setText(txt string in)		
		text = txt;
		setAttribute("value", txt);
		setBidiStyles();
		reverseText();		
	end
	
	private function getText() returns (string)
		if(this.textLayoutThis == "Visual")
			this.cleanBiDiMarkers();
		end
		reverseText();
		text = getAttribute("value");
		return (text);
	end

	function setReverseTextDirection( reverseTextDirection String in) { @Override }	
		this.reverseTextDirectionThis = reverseTextDirection;
		reverseText();		
	end
	
	function getReverseTextDirection() returns (String) { @Override }
		return (this.reverseTextDirectionThis);
	end

	function getWidgetOrientation() returns (String) { @Override }
		return (this.widgetOrientationThis);
	end
	
	function setWidgetOrientation(widgetOrientation String in) { @Override }
		this.widgetOrientationThis = widgetOrientation;
		setBidiStyles();		
	end
			
	function setTextLayout(textLayout String in) { @Override }		
		this.textLayoutThis = textLayout;
		setBidiStyles();	

		if(handlers == null)
			handlers = new VisualHandlers{};
		end			
		handlers.AttachHandlers(this);
		reverseText();
	end
		
	function getTextLayout() returns (String) { @Override }
		return (this.textLayoutThis);
	end
	
	function setBidiStyles()
		isVisual boolean = this.textLayoutThis == "Visual";
		isWidgetOrientationRTL boolean = this.widgetOrientationThis == "RTL";		
		this.setBiDiStyles(isVisual,isWidgetOrientationRTL);		
	end
	
	function reverseText()
		isVisual boolean = this.textLayoutThis == "Visual";
		isReverseDirection boolean = this.reverseTextDirectionThis == "Yes";					
		if(isVisual && isReverseDirection)
			if(handlers == null)
				handlers = new VisualHandlers{};
			end		
			handlers.ReverseText(this);
		end
	end			
end
