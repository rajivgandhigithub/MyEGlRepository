/*
 * Licensed Materials - Property of IBM
 *
 * Copyright IBM Corporation 2008, 2011. All Rights Reserved.
 *
 * U.S. Government Users Restricted Rights - Use, duplication or
 * disclosure restricted by GSA DP Schedule Contract with IBM Corp.
 */
package com.ibm.egl.rui.history;

import egl.javascript.Job;
import egl.ui.rui.Widget;

Delegate 
	HistoryChanged(newLocation String in)
end

Delegate
	OnBeforeUnloadMessageFunction() returns(String)
end

handler History type RUIWidget { onConstructionFunction = start }
   
    private frame Widget;
    private listeners HistoryChanged[] = [];
    private checkURLJob Job { runFunction = checkURL };
    private currentLocation String;
    private browser HistoryHelper{};
    private const BASE String = "com/ibm/egl/rui/history/history.html?history=";
    
    private function start()
    	if (!browser.inVisualEditorDesignMode())
	    	if (browser.isIE())
		    	frame = new Widget { tagName="iframe", visibility="hidden", position="absolute", width=0, height=0 };
		    	document.body.appendChild(frame);
		    	frame.setAttribute("src", BASE+"empty");
		    end
	   		checkURLJob.repeat(100);
	   	end
    end
    
    function goBack() 
    	browser.goBack();
    end
    
    function goForward()
    	browser.goForward();
    end
    
    function addListener(listener HistoryChanged)
    	if (!browser.inVisualEditorDesignMode())
	    	listeners ::= listener;
	    end
    end
    
    function addToHistory(newLocation String in)
     	if (!browser.inVisualEditorDesignMode())
	    	if (newLocation == "")
	    		messageValue String = Syslib.getMessage("CRRUI2004E", [newLocation]);
				throw new RuntimeException {message = messageValue, messageId = "CRRUI2004E"};
	    	end
	    	browser.setLocationHash(newLocation);
	    	checkUrl();
	    	if (browser.isIE())
	    		frame.setAttribute("src", BASE+newLocation);
	    	end
	    end
    end
    
    private function tellListeners()
    	for (n int from 1 to listeners.getSize() by 1)
    		listeners[n](ServiceLib.convertFromURLEncoded(currentLocation));
    	end
    end
    
    private function checkURL()
		if (browser.isIE()) 
	    	location String = browser.getFrameSrc(frame);
    		index int = strlib.indexOf(location, "=");
	    	location = location[index+1 : strlib.characterlen(location)];
	    	if (location != null && location != currentLocation)
	    		currentLocation = location;
	    		browser.setLocationHash(location);
	    		tellListeners();
		    end
	    else
			hash String = browser.getLocationHash();
		    if (hash != currentLocation)
		    	currentLocation = hash;
	    		tellListeners();
		    end
    	end
    end
    
    function keepUserOnPage(onBeforeUnloadMessageFunction OnBeforeUnloadMessageFunction)
    	if (!browser.inVisualEditorDesignMode())
	    	browser.setOnbeforeUnload(onBeforeUnloadMessageFunction);
	    end
    end
end
